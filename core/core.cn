: .l
  << -- >>
  10 write-byte
  end
; define-function

: .i
  << -- >>
  write-integer
  end
; define-function

: word:if?
  << word[address, length] -- bool >>
  "if" string-equal?
  end
; define-function

: word:else?
  << word[address, length] -- bool >>
  "else" string-equal?
  end
; define-function

: word:then?
  << word[address, length] -- bool >>
  "then" string-equal?
  end
; define-function

: syntax,if,make-jojo
  << string[address, length], word:if --
     address, string[address, length] >>
  drop2
  *false?branch* save-into,jo-area
  *current-free-address,jo-area* xx|swap|x
  0 save-into,jo-area
  end
; define-function

: syntax,else,make-jojo
  << address, string[address, length], word:else --
     address, string[address, length] >>
  drop2
  *branch* save-into,jo-area
  x|swap|xx
  *current-free-address,jo-area* xxx|swap|x
  0 save-into,jo-area
  << address, string[address, length], address >>
  *current-free-address,jo-area*
  over sub *jo-size* div
  swap save
  end
; define-function

: syntax,then,make-jojo
  << address, string[address, length], word:then --
     string[address, length] >>
  drop2
  x|swap|xx
  *current-free-address,jo-area*
  over sub *jo-size* div
  swap save
  end
; define-function

: more,rule-set,make-jojo
  << -- >>
  jo word:if?   jo syntax,if,make-jojo    *rule-set,make-jojo* add-rule
  jo word:else? jo syntax,else,make-jojo  *rule-set,make-jojo* add-rule
  jo word:then? jo syntax,then,make-jojo  *rule-set,make-jojo* add-rule
  end
; define-function

more,rule-set,make-jojo

: word:prepare-for?
  << word[address, length] -- bool >>
  "prepare-for" string-equal?
  end
; define-function

: syntax,prepare-for,make-jojo
  << string[address, length], word:prepare-for --
     string[address, length] >>
  drop2
  *prepare-for*    save-into,jo-area
  *exception-head* save-into,jo-area
  end
; define-function

: more,rule-set,make-jojo
  << -- >>
  jo word:prepare-for?
  jo syntax,prepare-for,make-jojo
  *rule-set,make-jojo* add-rule
  end
; define-function

more,rule-set,make-jojo

: remove-char!,loop
  << cursor, length, char -- cursor >>
  >:char
  >:length
  >:cursor
  :length zero? if
    :cursor
    end
  then
  :cursor fetch-byte :char equal? if
    :cursor add1 :length sub1
    :cursor
    string->buffer!
    :cursor
    :length sub1
    :char
  else
    :cursor add1
    :length sub1
    :char
  then
  <> remove-char!,loop
; define-function

: remove-char!
  << string[address, length], char -- string[address, length] >>
  x|over|xx >:address
  remove-char!,loop >:cursor
  :address
  :cursor :address sub
  end
; define-function

: latin-char?
  << char -- bool >>
  dup "A" string-head,char less-than? false?branch 4
    drop false
    end
  dup "Z" string-head,char less-or-equal? false?branch 4
    drop true
    end
  dup "a" string-head,char less-than? false?branch 4
    drop false
    end
  dup "z" string-head,char less-or-equal? false?branch 4
    drop true
    end
  drop false
  end
; define-function

: latin-char->number
  << latin-char -- number >>
  dup "A" string-head,char less-than? if
    "* (latin-char->number) the argument must be a latin-char" .s .l
    "  but the following char is less-than 'A' : " .s
    .i .l
    end
  then
  dup "Z" string-head,char less-or-equal? if
    "A" string-head,char
    sub
    10 add
    end
  then
  dup "a" string-head,char less-than? if
    "* (latin-char->number) the argument must be a latin-char" .s .l
    "  but the following char is less-than 'a' but greater-then 'Z' : " .s
    .i .l
    end
  then
  dup "z" string-head,char less-or-equal? if
    "a" string-head,char
    sub
    10 add
    end
  then
  "* (latin-char->number) the argument must be a latin-char" .s .l
  "  but the following char is greater-then 'z' : " .s
  .i .l
  end
; define-function

: number->latin-char
  << number -- latin-char >>
  10 sub
  "a" string-head,char
  add
  end
; define-function

: wild-digit-string?
  << string[address, length] -- bool >>
  dup zero? if
    drop2 true
    end
  then
  over fetch-byte
  dup digit-char?
  swap latin-char?
  or if
    string-tail,char
    <> wild-digit-string?
  then
  drop2
  false
  end
; define-function

: wild-integer-string?
  << string[address, length] -- bool >>
  dup zero? if
    drop2 false
    end
  then
  dup2 string-head,char
  "-" string-head,char
  equal? if
    string-tail,char
    wild-digit-string?
    end
  then
  wild-digit-string?
  end
; define-function

: empty-string?
  << string[address, length] -- bool >>
  swap drop
  zero?
  end
; define-function

: base#wild-integer-string?
  << string[address, length] -- bool >>
  128 allocate-local-memory
  >:string-address
  tuck
  :string-address
  string->buffer!
  :string-address swap
  "_" string-head,char
  remove-char!
  >:new-string-length
  >:new-string-address
  << dup2 .s .l 0 end >>
  :new-string-address
  :new-string-length
  "#" string-head,char
  string,find-char if
  else
    false
    end
  then
  >:address-of-#
  :new-string-address
  :address-of-# :new-string-address sub
  >::base-string
  :address-of-# add1
  :address-of-# :new-string-address sub add1
  :new-string-length swap sub
  >::wild-integer-string
  ::base-string digit-string?
  ::base-string empty-string? not
  and if
  else
    false
    end
  then
  ::wild-integer-string wild-integer-string?
  ::wild-integer-string empty-string? not
  and if
    true
  else
    false
  then
  end
; define-function

: base#wild-integer-string->base-string
  << string[address, length] -- string[address, length] >>
  >:length
  >:address
  :address
  :length
  "#" string-head,char
  string,find-char if
  else
    "* (base#wild-integer-string->base-string)" .s .l
    "  the argument must be a base#wild-integer-string" .s .l
    "  but the following string does not even have a '#' in it :" .s .l
    "  " .s
    :address :length .s .l
    << to balance the argument-stack or not ??? >>
    << :address :length >>
    end
  then
  >:address-of-#
  :address
  :address-of-# :address sub
  end
; define-function

: base#wild-integer-string->wild-integer-string
  << string[address, length] -- string[address, length] >>
  >:length
  >:address
  :address
  :length
  "#" string-head,char
  string,find-char if
  else
    "* (base#wild-integer-string->wild-integer-string)" .s .l
    "  the argument must be a base#wild-integer-string" .s .l
    "  but the following string does not even have a '#' in it :" .s .l
    "  " .s
    :address :length .s .l
    << to balance the argument-stack or not ??? >>
    << :address :length >>
    end
  then
  >:address-of-#
  :address-of-# add1
  :address-of-# :address sub add1
  :length swap sub
  end
; define-function

: wild-integer-string->integer,with-base,loop
  << string[address, length], base, sum, counter -- integer >>
  >:counter
  >:sum
  >:base
  >:length
  >:address
  :length zero? if
    :sum
    end
  then
  :address fetch-byte >:char
  :char digit-char? if
    :char digit-char->number
  then
  :char latin-char? if
    :char latin-char->number
  then
  :base :counter power
  mul
  :sum add
  >:sum
  :address add1
  :length sub1
  :base
  :sum
  :counter add1
  <> wild-integer-string->integer,with-base,loop
; define-function

: wild-integer-string->integer,with-base
  << string[address, length], base -- integer >>
  >:base
  dup zero? if
    drop2
    0
    end
  then
  dup2 string-head,char
  "-" string-head,char
  equal? if
    string-tail,char
    -1 >:sign
  else
    1 >:sign
  then
  >::string
  ::string string-reverse!
  :base
  0 0 wild-integer-string->integer,with-base,loop
  :sign mul
  ::string string-reverse!
  drop2
  end
; define-function

: base#wild-integer-string->integer
  << string[address, length] -- integer >>
  128 allocate-local-memory
    >:address
  tuck :address string->buffer!
    >:length
  :address :length
  "_" string-head,char remove-char!
    >::string
  ::string
  base#wild-integer-string->base-string
    >::base-string
  ::string
  base#wild-integer-string->wild-integer-string
    >::wild-integer-string
  ::base-string
  digit-string->number
    >:base
  ::wild-integer-string
  :base
  wild-integer-string->integer,with-base
  end
; define-function

: .#,loop
  << number, base, cursor -- cursor >>
  >:cursor
  >:base
  >:number
  :number zero? if
    :cursor
    end
  then
  :number
  :base
  divmod >:mod >:div
  :mod 10 less-than? if
    :mod number->digit-char
  else
    :mod number->latin-char
  then
  :cursor
  save-byte
  :div
  :base
  :cursor add1
  <> .#,loop
; define-function

: .#
  << integer, base -- >>
  over zero? if
    drop .i
    end
  then
  dup 36 greater-than?
  over 2 less-than?
  or if
    "* (.#) the base " .s .i " is not valid to write a number" .s .l
    "  a base should in between 2 and 36 includingly" .s .l
    "  the integer to be written is " .s .i .l
    end
  then
  dup .i
  "#" .s
  over negative? if
    swap negate swap
    "-" .s
  then
  128 allocate-local-memory >:buffer
  :buffer
  .#,loop >:cursor
  :buffer
  :cursor :buffer sub
  string-reverse! .s
  end
; define-function

: .#2  2  .# " " .s end ; define-function
: .#8  8  .# " " .s end ; define-function
: .#16 16 .# " " .s end ; define-function

: number->gamber
  << number -- gamber >>
  dup 1 bit-right
  bit-xor
  end
; define-function

: gamber->number,loop
  << gamber, number, cursor -- number >>
  dup negative? if
    drop
    swap drop
    end
  then
  >:cursor
  >:number
  >:gamber
  :gamber :cursor get-bit
  :number :cursor add1 get-bit
  xor if
    :number :cursor set-bit >:number
  then
  :gamber
  :number
  :cursor sub1
  <> gamber->number,loop
; define-function

: gamber->number
  << gamber -- number >>
  dup find-highest-set-bit
  dup negative? if
    drop
    end
  then
  0 over set-bit
  swap sub1
  gamber->number,loop
  end
; define-function

: syntax,base#wild-integer-string,make-jojo
  << string[address, length], word[address, length] --
     string[address, length] >>
  *literal* save-into,jo-area
  base#wild-integer-string->integer
  save-into,jo-area
  end
; define-function

: more,rule-set,make-jojo
  << -- >>
  jo base#wild-integer-string?
  jo syntax,base#wild-integer-string,make-jojo
  *rule-set,make-jojo* add-rule
  end
; define-function

more,rule-set,make-jojo

: allocate-memory
  << size -- address >>
  dup *current-free-address,un-initialized-memory* clear-memory
  *current-free-address,un-initialized-memory* swap << address as return value >>
  address *current-free-address,un-initialized-memory* add-save
  end
; define-function

1024 *jo-size* mul
: *size,rule-set,basic-REPL*
; define-variable

<< for cursor >>
*jo-size* allocate-memory drop

*size,rule-set,basic-REPL*
allocate-memory
: *rule-set,basic-REPL*
; define-variable

*rule-set,basic-REPL*
*rule-set,basic-REPL* *jo-size* sub
save

: execute-word
  << word[address, length] -- unknown >>
  dup2
  find if
    dup exception-jo? if
      drop
      "* (execute-word) can not execute exception directly : " .s
      .s .l
      end
    then
    << function & primitive-function & variable >>
    xx|swap|x drop2
    execute-jo
    end
  else
  "* (execute-word) meets undefined word : " .s
  .s .l
  then
  end
; define-function

: word:bye?
  << word[address, length] -- bool >>
  "bye" string-equal?
  end
; define-function

: !bye,basic-REPL
  << -- >>
  drop-syntax-stack
  end
; define-exception

: syntax,bye,basic-REPL
  << word:bye -- >>
  drop2
  !bye,basic-REPL
; define-function

: word:jo?
  << word[address, length] -- bool >>
  "jo" string-equal?
  end
; define-function

: syntax,jo,basic-REPL
  << word:jo -- >>
  drop2
  read-word-for-REPL
  >::word
  ::word find if
    end
  then
  "* (syntax,jo,basic-REPL) meet undefined word after jo : " .s ::word .s .l
  end
; define-function

: init,rule-set,basic-REPL
  << -- >>
  jo integer-string? jo string->integer       *rule-set,basic-REPL* add-rule
  jo word:bye?       jo syntax,bye,basic-REPL *rule-set,basic-REPL* add-rule
  jo word:jo?        jo syntax,jo,basic-REPL  *rule-set,basic-REPL* add-rule
  jo base#wild-integer-string? jo base#wild-integer-string->integer *rule-set,basic-REPL* add-rule
  end
; define-function

init,rule-set,basic-REPL

: basic-REPL,loop
  << unknown -- unknown >>
  read-word-for-REPL
  dup2 find-syntax if
    execute-jo
  else
    execute-word
  then
  <> basic-REPL,loop
; define-function

: basic-REPL
  << unknown -- unknown >>
  prepare-for
    !bye,basic-REPL
    end
  *rule-set,basic-REPL* push-syntax-stack
  basic-REPL,loop
  end
; define-function

basic-REPL

: syntax,if,basic-REPL,meet-true,else
  << -- >>
  read-word-for-REPL
  "then" string-equal? if
    end
  then
  <> syntax,if,basic-REPL,meet-true,else
; define-function

: syntax,if,basic-REPL,meet-true
  << -- >>
  read-word-for-REPL
  dup2 "then" string-equal? if
    drop2
    end
  then
  dup2 "else" string-equal? if
    drop2
    syntax,if,basic-REPL,meet-true,else
    end
  then
  dup2 find-syntax if
    execute-jo
  else
    execute-word
  then
  <> syntax,if,basic-REPL,meet-true
; define-function

: syntax,if,basic-REPL,meet-false,else
  << -- >>
  read-word-for-REPL
  dup2 "then" string-equal? if
    drop2
    end
  then
  dup2 find-syntax if
    execute-jo
  else
    execute-word
  then
  <> syntax,if,basic-REPL,meet-false,else
; define-function

: syntax,if,basic-REPL,meet-false
  << -- >>
  read-word-for-REPL
  dup2 "then" string-equal? if
    drop2
    end
  then
  dup2 "else" string-equal? if
    drop2
    syntax,if,basic-REPL,meet-false,else
    end
  then
  drop2
  <> syntax,if,basic-REPL,meet-false
; define-function

: syntax,if,basic-REPL
  << bool, word:if -- >>
  drop2 if
    syntax,if,basic-REPL,meet-true
  else
    syntax,if,basic-REPL,meet-false
  then
  end
; define-function

jo word:if? jo syntax,if,basic-REPL *rule-set,basic-REPL* add-rule

1024 1024 mul
: *circular-string-area,size*
; define-variable


*circular-string-area,size* allocate-memory
: *circular-string-area*
; define-variable

*circular-string-area*
: *current-free-address,circular-string-area*
; define-variable

: save-into,circular-string-area
  << string[address, length] -- >>
  dup *current-free-address,circular-string-area* add
  *circular-string-area,size* *circular-string-area* add
  greater-or-equal? if
    *circular-string-area*
    address *current-free-address,circular-string-area*
    save
  then
  tuck
  *current-free-address,circular-string-area*
  string->buffer!
  address *current-free-address,circular-string-area*
  add-save
  0 *current-free-address,circular-string-area*
  save-byte
  1 address *current-free-address,circular-string-area*
  add-save
  end
; define-function

: syntax,double-quote,basic-REPL,loop
  << cursor -- cursor >>
  read-byte
  dup 34 equal? if
    drop
    end
  then
  over save-byte
  add1
  <> syntax,double-quote,basic-REPL,loop
; define-function

: syntax,double-quote,basic-REPL
  << word:double-quote -- string[address, length] >>
  drop2
  1024 2 mul allocate-local-memory >:buffer
  :buffer
  syntax,double-quote,basic-REPL,loop
  >:cursor
  *current-free-address,circular-string-area* >:address
  :buffer
  :cursor :buffer sub dup >:length
  save-into,circular-string-area
  :address
  :length
  end
; define-function

jo word:double-quote? jo syntax,double-quote,basic-REPL *rule-set,basic-REPL* add-rule

0
: *syntax,address,basic-REPL,stack-balancer*
; define-variable

: syntax,address,basic-REPL
  << word:address -- address >>
  drop2
  read-word-for-REPL
  >::word
  ::word find if
  else
    "* (syntax,address,basic-REPL) meet undefined word : " .s ::word .s .l
    address *syntax,address,basic-REPL,stack-balancer*
    end
  then
  >:jo
  :jo variable-jo? if
  else
    "* (syntax,address,basic-REPL) meet a not variable-jo : " .s ::word .s .l
    address *syntax,address,basic-REPL,stack-balancer*
    end
  then
  :jo *jo-size* add
  end
; define-function

jo word:address? jo syntax,address,basic-REPL *rule-set,basic-REPL* add-rule

: print-argument-stack,loop
  << address, counter -- >>
  dup zero? if
    drop2
    end
  then
  sub1 swap
    dup fetch .
  *jo-size* add
  swap
  <> print-argument-stack,loop
; define-function

: print-argument-stack
  << -- >>
  snapshot-the-stack-pointer
  *the-stack-pointer-snapshot*
  *the-stack* greater-or-equal? if
    *the-stack*   << address as return value >>
    *the-stack-pointer-snapshot* *the-stack* sub
    *jo-size* div << counter as return value >>
    print-argument-stack,loop
    end
  then
  "below the stack " .s
  end
; define-function

: print-argument-stack,pretty
  << -- >>
  snapshot-the-stack-pointer
  .l
  "  * " .s
     *the-stack-pointer-snapshot*
     *the-stack* sub
     << ad hoc for the BUG of div >>
     dup negative? if
       negate
       *jo-size* div
       negate
     else
       *jo-size* div
     then
     .i
  " * " .s
  " -- " .s
    print-argument-stack
  "--" .s
  .l
  end
; define-function

: stack-REPL,loop
  << unknown -- unknown >>
  read-word-for-REPL
  dup2 find-syntax if
    execute-jo
  else
    execute-word
  then
  print-argument-stack,pretty
  <> stack-REPL,loop
; define-function

: stack-REPL
  << unknown -- unknown >>
  prepare-for
    !bye,basic-REPL
    end
  *rule-set,basic-REPL* push-syntax-stack
  stack-REPL,loop
  end
; define-function

*jo-size* 4 equal? if
  5   : :syscall-number,open:       ; define-variable
  6   : :syscall-number,close:      ; define-variable
  3   : :syscall-number,read:       ; define-variable
  4   : :syscall-number,write:      ; define-variable
  9   : :syscall-number,link:       ; define-variable
  10  : :syscall-number,unlink:     ; define-variable
  39  : :syscall-number,mkdir:      ; define-variable
  40  : :syscall-number,rmdir:      ; define-variable
  141 : :syscall-number,getdents:   ; define-variable
  220 : :syscall-number,getdents64: ; define-variable
  106 : :syscall-number,stat:       ; define-variable
  1   : :syscall-number,exit:       ; define-variable
  43  : :syscall-number,times:      ; define-variable
  20  : :syscall-number,getpid:     ; define-variable
  183 : :syscall-number,getcwd:     ; define-variable
  12  : :syscall-number,chdir:      ; define-variable
then

*jo-size* 8 equal? if
  2   : :syscall-number,open:       ; define-variable
  3   : :syscall-number,close:      ; define-variable
  0   : :syscall-number,read:       ; define-variable
  1   : :syscall-number,write:      ; define-variable
  86  : :syscall-number,link:       ; define-variable
  87  : :syscall-number,unlink:     ; define-variable
  83  : :syscall-number,mkdir:      ; define-variable
  84  : :syscall-number,rmdir:      ; define-variable
  78  : :syscall-number,getdents:   ; define-variable
  217 : :syscall-number,getdents64: ; define-variable
  4   : :syscall-number,stat:       ; define-variable
  60  : :syscall-number,exit:       ; define-variable
  100 : :syscall-number,times:      ; define-variable
  39  : :syscall-number,getpid:     ; define-variable
  79  : :syscall-number,getcwd:     ; define-variable
  80  : :syscall-number,chdir:      ; define-variable
then

: error-code->string
  << error-code -- string[address, length] >>
  dup -1  equal? if drop  << EPERM   >>   "Operation not permitted " end then
  dup -2  equal? if drop  << ENOENT  >>   "No such file or directory " end then
  dup -3  equal? if drop  << ESRCH   >>   "No such process " end then
  dup -4  equal? if drop  << EINTR   >>   "Interrupted system call " end then
  dup -5  equal? if drop  << EIO     >>   "I/O error " end then
  dup -6  equal? if drop  << ENXIO   >>   "No such device or address " end then
  dup -7  equal? if drop  << E2BIG   >>   "Argument list too long " end then
  dup -8  equal? if drop  << ENOEXEC >>   "Exec format error " end then
  dup -9  equal? if drop  << EBADF   >>   "Bad file number " end then
  dup -10 equal? if drop  << ECHILD  >>   "No child processes " end then
  dup -11 equal? if drop  << EAGAIN  >>   "Try again " end then
  dup -12 equal? if drop  << ENOMEM  >>   "Out of memory " end then
  dup -13 equal? if drop  << EACCES  >>   "Permission denied " end then
  dup -14 equal? if drop  << EFAULT  >>   "Bad address " end then
  dup -15 equal? if drop  << ENOTBLK >>   "Block device required " end then
  dup -16 equal? if drop  << EBUSY   >>   "Device or resource busy " end then
  dup -17 equal? if drop  << EEXIST  >>   "File exists " end then
  dup -18 equal? if drop  << EXDEV   >>   "Cross-device link " end then
  dup -19 equal? if drop  << ENODEV  >>   "No such device " end then
  dup -20 equal? if drop  << ENOTDIR >>   "Not a directory " end then
  dup -21 equal? if drop  << EISDIR  >>   "Is a directory " end then
  dup -22 equal? if drop  << EINVAL  >>   "Invalid argument " end then
  dup -23 equal? if drop  << ENFILE  >>   "File table overflow " end then
  dup -24 equal? if drop  << EMFILE  >>   "Too many open files " end then
  dup -25 equal? if drop  << ENOTTY  >>   "Not a typewriter " end then
  dup -26 equal? if drop  << ETXTBSY >>   "Text file busy " end then
  dup -27 equal? if drop  << EFBIG   >>   "File too large " end then
  dup -28 equal? if drop  << ENOSPC  >>   "No space left on device " end then
  dup -29 equal? if drop  << ESPIPE  >>   "Illegal seek " end then
  dup -30 equal? if drop  << EROFS   >>   "Read-only file system " end then
  dup -31 equal? if drop  << EMLINK  >>   "Too many links " end then
  dup -32 equal? if drop  << EPIPE   >>   "Broken pipe " end then
  dup -33 equal? if drop  << EDOM    >>   "Math argument out of domain of func " end then
  dup -34 equal? if drop  << ERANGE  >>   "Math result not representable " end then
  "unknow error-code : " .s .i .l
  "* (error-code->string) fail" << to balance the argument-stack >>
  end
; define-function

: file-type-code->string
  << file-type-code -- string[address, length] >>
  dup 1  equal?  << DT_FIFO  >>  if drop "named pipe i.e. FIFO"  end then
  dup 2  equal?  << DT_CHR   >>  if drop "character device"      end then
  dup 4  equal?  << DT_DIR   >>  if drop "directory"             end then
  dup 6  equal?  << DT_BLK   >>  if drop "block device"          end then
  dup 8  equal?  << DT_REG   >>  if drop "regular file"          end then
  dup 10 equal?  << DT_LNK   >>  if drop "symbolic link"         end then
  dup 12 equal?  << DT_SOCK  >>  if drop "UNIX domain socket"    end then
  drop
  "unknow file type"
  end
; define-function

: jotionary,list-by-jo-predicate,loop
  << jo-predicate, counter, jo -- total >>
  dup zero? if
    drop
    swap drop
    end
  then
  dup x|over|xxx execute-jo false? if
    jo->pre-jo
    <> jotionary,list-by-jo-predicate,loop
  then
  swap
    "  " .s
    add1 dup .i ". " .s
  swap
  dup jo->name
  dup2 space-string? if
    drop2
    "  unnamed" .s .l
  else
    .s .l
  then
  jo->pre-jo
  <> jotionary,list-by-jo-predicate,loop
; define-function

: jotionary,list-by-jo-predicate
  << jo-predicate -- total >>
  0 *first-jo-in-jotionary*
  jotionary,list-by-jo-predicate,loop
  end
; define-function

: jotionary,report
  << -- >>
  "* all primitive-function in jotionary :" .s .l
  jo primitive-function-jo?
  jotionary,list-by-jo-predicate
  "* all function in jotionary :" .s .l
  jo function-jo?
  jotionary,list-by-jo-predicate add
  "* all exception in jotionary :" .s .l
  jo exception-jo?
  jotionary,list-by-jo-predicate add
  "* all variable in jotionary :" .s .l
  jo variable-jo?
  jotionary,list-by-jo-predicate add
  "* totally : " .s
  .i .l
  end
; define-function

: rule-set,list,loop
  << rule-set, cursor, counter -- >>
  xx|over|x equal? if
    drop drop2
    end
  then
  "  * " .s
  "(" .s
  add1 dup .i
  ")" .s .l
  swap
    dup cursor->predicate
    "    " .s jo->name .s .l
    dup cursor->function
    "    " .s jo->name .s .l
    *jo-size* sub *jo-size* sub
  swap
  <> rule-set,list,loop
; define-function

: rule-set,list
  << rule-set -- >>
  dup rule-set,fetch-cursor
  0 rule-set,list,loop
  end
; define-function

: input-port,open
  << path[address, length]
     -- port, true
     -- error-code, false >>
  string->syscall-string
  >:syscall-path
  0
  0 <<
  O_RDONLY
  >>
  :syscall-path
  :syscall-number,open:
  3 syscall
  dup
  negative? if
    false
    end
  then
  true
  end
; define-function

: output-port,creat
  << path[address, length]
     -- port, true
     -- error-code, false >>
  string->syscall-string
  >:syscall-path
  8#644 <<
  2#110100100
  >>
  8#1102 <<
  O_RDWR   0002
  O_CREAT  0100
  O_TRUNC  1000
  >>
  :syscall-path
  :syscall-number,open:
  3 syscall
  dup
  negative? if
    false
    end
  then
  true
  end
; define-function

: output-port,update
  << path[address, length]
     -- port, true
     -- error-code, false >>
  string->syscall-string
  >:syscall-path
  8#644 <<
  110100100b
  >>
  8#1002 <<
  O_RDWR   0002
  O_TRUNC  1000
  >>
  :syscall-path
  :syscall-number,open:
  3 syscall
  dup
  negative? if
    false
    end
  then
  true
  end
; define-function

: port,close
  << port
     -- true
     -- error-code, false >>
  :syscall-number,close:
  1 syscall
  dup
  negative? if
    false
    end
  then
  drop true
  end
; define-function

: port,read
  << buffer, max-size, port
     -- counter, true
     -- error-code, false >>
  >:port
  swap
  :port
  :syscall-number,read:
  3 syscall
  dup
  negative? if
    false
    end
  then
  true
  end
; define-function

: port,write
  << buffer, max-size, port
     -- counter, true
     -- error-code, false >>
  >:port
  swap
  :port
  :syscall-number,write:
  3 syscall
  dup
  negative? if
    false
    end
  then
  true
  end
; define-function

: file,creat
  << path[address, length] -- >>
  >::path
  ::path
  string->syscall-string
  >:syscall-path
  8#644 <<
  2#110100100
  >>
  8#0200
  8#0100 bit-or <<
  O_EXCL   0200
  O_CREAT  0100
  >>
  :syscall-path
  :syscall-number,open:
  3 syscall
  dup
  negative? if
    "* (file,creat) fail to open port for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  port,close if
    end
  then
  "* (file,creat) fail to close port for the following path :" .s .l
  "  " .s ::path .s .l
  "  " .s error-code->string .s .l
  end
; define-function

: file,update
  << string[address, length], path[address, length] -- >>
  >::path
  >::string
  ::path
  string->syscall-string
  >:syscall-path
  8#644 <<
  2#110100100
  >>
  8#1002 <<
  O_RDWR   0002
  O_TRUNC  1000
  >>
  :syscall-path
  :syscall-number,open:
  3 syscall
  dup
  negative? if
    "* (file,update) fail to open port for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  >:port
  ::string swap
  :port
  :syscall-number,write:
  3 syscall
  dup
  negative? if
    "* (file,update) fail to write port for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  >:counter << bytes been written >>
  :port
  port,close if
    end
  then
  "* (file,update) fail to close port for the following path : " .s .l
  "  " .s ::path .s .l
  "  " .s error-code->string .s .l
  end
; define-function

: file,remove
  << path[address, length] -- >>
  >::path
  ::path
  string->syscall-string
  >:syscall-path
  :syscall-path
  :syscall-number,unlink:
  1 syscall
  dup
  negative? if
    "* (file,remove) fail for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  drop
  end
; define-function

: directory,creat
  << path[address, length] -- >>
  >::path
  ::path
  string->syscall-string
  >:syscall-path
  8#755 <<
  2#111101101
  >>
  :syscall-path
  :syscall-number,mkdir:
  2 syscall
  dup
  negative? if
    "* (directory,creat) fail for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  drop
  end
; define-function

: directory,remove
  << path[address, length] -- >>
  >::path
  ::path
  string->syscall-string
  >:syscall-path
  :syscall-path
  :syscall-number,rmdir:
  1 syscall
  dup
  negative? if
    "* (directory,remove) fail for the following path :" .s .l
    "  " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  drop
  end
; define-function

0 : +getdents64,d_ino+        ; define-variable
0 : +getdents64,d_off+        ; define-variable
0 : +getdents64,d_reclen+     ; define-variable
0 : +getdents64,d_type+       ; define-variable
0 : +getdents64,d_name+       ; define-variable
0 : +getdents64,end+          ; define-variable

: init,getdents64
  << offset -- >>
  *jo-size* 4 equal? if
    dup address +getdents64,d_ino+       save 8 add
    dup address +getdents64,d_off+       save 8 add
    dup address +getdents64,d_reclen+    save 2 add
    dup address +getdents64,d_type+      save 1 add
    dup address +getdents64,d_name+      save 256 add
        address +getdents64,end+           save
    end
  then
  *jo-size* 8 equal? if
    dup address +getdents64,d_ino+       save 8 add
    dup address +getdents64,d_off+       save 8 add
    dup address +getdents64,d_reclen+    save 2 add
    dup address +getdents64,d_type+      save 1 add
    dup address +getdents64,d_name+      save 256 add
        address +getdents64,end+           save
    end
  then
  end
; define-function

0 init,getdents64
+getdents64,end+ : +getdents64,length+ ; define-variable

: empty-directory?
  << path[address, length] -- bool >>
  >::path
  ::path input-port,open if
  else
    "* (empty-directory?) fail to open : " .s ::path .s .l
    "  " .s error-code->string .s .l
    false
    end
  then
  >:port
  128 allocate-local-memory >:getdents64-structure-list
  128
  :getdents64-structure-list
  :port
  :syscall-number,getdents64:
  3 syscall
  48 equal? << this call to equal? returns the needed bool >>
  :port port,close if
    end
  then
  "* (empty-directory?) fail to close : " .s ::path .s .l
  "  " .s error-code->string .s .l
  false
  end
; define-function

: directory,list-by-file-type-code,loop,print
  << end, cursor, file-type-code -- >>
  >:file-type-code
  dup2 less-or-equal? if
    drop2
    end
  then
  >:cursor
  :cursor init,getdents64
  +getdents64,d_type+ fetch-byte
  :file-type-code equal? if
    "  " .s
    +getdents64,d_name+
    dup 256
    0 string,find-char
    drop  << drop 0 >>
    over sub .s .l
  then
  :cursor
  +getdents64,d_reclen+ fetch-two-bytes
  add
  :file-type-code
  <> directory,list-by-file-type-code,loop,print
; define-function

: directory,list-by-file-type-code,loop
  << port, file-type-code -- >>
  >:file-type-code
  >:port
  1024 allocate-local-memory >:getdents64-structure-list
  1024
  :getdents64-structure-list
  :port
  :syscall-number,getdents64:
  3 syscall
  dup negative? if
    error-code->string .s .l
    end
  then
  dup zero? if
    drop
    end
  then
  :getdents64-structure-list add
  :getdents64-structure-list
  :file-type-code
  directory,list-by-file-type-code,loop,print
  :port
  :file-type-code
  <> directory,list-by-file-type-code,loop
; define-function

: directory,list-by-file-type-code
  << directory-path[address, length], file-type-code -- >>
  >:file-type-code
  >::path
  ::path input-port,open if
  else
    "* (directory,list-by-file-type-code) fail to open : " .s ::path .s .l
    "  " .s error-code->string .s .l
    end
  then
  >:port
  :port :file-type-code directory,list-by-file-type-code,loop
  :port port,close if
    end
  then
  "* (directory,list-by-file-type-code) fail to close : " .s ::path .s .l
  "  " .s error-code->string .s .l
  end
; define-function

: directory,list-file
  << directory-path[address, length] -- >>
  8 directory,list-by-file-type-code
  end
; define-function

: directory,list-directory
  << directory-path[address, length] -- >>
  4 directory,list-by-file-type-code
  end
; define-function

: directory,find-by-file-type-code,loop,find
  << end, cursor, file-name[address, length], file-type-code -- bool >>
  >:file-type-code
  >::file-name
  dup2 less-or-equal? if
    drop2
    false
    end
  then
  >:cursor
  >:end
  :cursor init,getdents64
  +getdents64,d_type+ fetch-byte :file-type-code equal? if
    ::file-name +getdents64,d_name+
    dup2 add fetch-byte zero? if
      over string-equal? if
        true
        end
      then
    else
      drop drop2
    then
  then
  :end
  :cursor +getdents64,d_reclen+ fetch-two-bytes add
  ::file-name
  :file-type-code
  <> directory,find-by-file-type-code,loop,find
; define-function

: directory,find-by-file-type-code,loop
  << port, file-name[address, length], file-type-code -- bool >>
  >:file-type-code
  >::file-name
  >:port
  1024 allocate-local-memory >:getdents64-structure-list
  1024
  :getdents64-structure-list
  :port
  :syscall-number,getdents64:
  3 syscall
  dup negative? if
    "* (directory,find-by-file-type-code,loop) syscall fail" .s .l
    "  " .s error-code->string .s .l
    false
    end
  then
  dup zero? if
    << zero as false >>
    end
  then
  :getdents64-structure-list add
  :getdents64-structure-list
  ::file-name
  :file-type-code
  directory,find-by-file-type-code,loop,find if
    true
    end
  then
  :port
  ::file-name
  :file-type-code
  <> directory,find-by-file-type-code,loop
; define-function

: directory,find-by-file-type-code
  << directory-path[address, length],
     file-name[address, length],
     file-type-code
     -- true
     -- false >>
  >:file-type-code
  >::file-name
  >::directory-path
  ::directory-path input-port,open if
  else
    "* (directory,find-by-file-type-code) fail to open : " .s ::directory-path .s .l
    "  " .s error-code->string .s .l
    false
    end
  then
  >:port
  :port ::file-name :file-type-code directory,find-by-file-type-code,loop
  :port port,close if
    end
  then
  "* (directory,find-by-file-type-code) fail to close : " .s ::directory-path .s .l
  "  " .s error-code->string .s .l
  end
; define-function

: directory,find-file
  << directory-path[address, length],
     file-name[address, length]
     -- true
     -- false >>
  8 directory,find-by-file-type-code
  end
; define-function

: directory,find-directory
  << directory-path[address, length],
     file-name[address, length]
     -- true
     -- false >>
  4 directory,find-by-file-type-code
  end
; define-function

0 : *pid* ; define-variable

: init-pid
  << -- >>
  :syscall-number,getpid:
  0 syscall
  address *pid* save
  end
; define-function

: get-pid
  << -- pid >>
  *pid*
  end
; define-function

512 allocate-memory
: *address,command-line*
; define-variable

512
: *length,command-line*
; define-variable

: init-command-line
  << -- >>
  64 allocate-local-memory >:path-buffer
  :path-buffer >:cursor

  get-pid write-number,fill-buffer >::pid-string

  "/proc/" dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  ::pid-string dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  "/cmdline" dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  :path-buffer
  :cursor :path-buffer sub dup2 >::path
  input-port,open if
  else
    "* (init-command-line) fail to open : " .s ::path .s .l
    end
  then
  >:port

  *address,command-line*
  *length,command-line*
  :port
  port,read if
  else
    "* (init-command-line) fail to read : " .s ::path .s .l
    end
  then
  address *length,command-line* save
  end
; define-function

: get-command-line
  << -- string[address, length] >>
  *address,command-line* *length,command-line*
  end
; define-function

1024 4 mul allocate-memory
: *address,environment-string-variable-list*
; define-variable

1024 4 mul
: *length,environment-string-variable-list*
; define-variable

: init-environment-string-variable-list
  << -- >>
  64 allocate-local-memory >:path-buffer
  :path-buffer >:cursor

  get-pid write-number,fill-buffer >::pid-string

  "/proc/" dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  ::pid-string dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  "/environ" dup >:add-to-cursor
  :cursor string->buffer!
  :add-to-cursor :cursor add >:cursor

  :path-buffer
  :cursor :path-buffer sub dup2 >::path
  input-port,open if
  else
    "* (init-environment-string-variable-list) fail to open : " .s ::path .s .l
    end
  then
  >:port

  *address,environment-string-variable-list*
  *length,environment-string-variable-list*
  :port
  port,read if
  else
    "* (init-environment-string-variable-list) fail to read : " .s ::path .s .l
    end
  then
  address *length,environment-string-variable-list* save
  end
; define-function

: get-environment-string-variable-list
  << -- string[address, length] >>
  *address,environment-string-variable-list* *length,environment-string-variable-list*
  end
; define-function

: find-environment-string-variable,loop
  << string[address, length], cursor
     -- string[address, length], true
     -- false >>
  dup
  get-environment-string-variable-list add
  greater-than? if
    drop drop2
    false
    end
  then
  >:cursor
  >:length
  >:address
  :address :cursor :length
  compare-buffer not if
    :address :length
    :cursor 0 cursor->next-matching-byte
    <> find-environment-string-variable,loop
  then
  :cursor :length fetch-byte
  "=" string-head,char
  equal? not if
    :address :length
    :cursor 0 cursor->next-matching-byte
    <> find-environment-string-variable,loop
  then
  :cursor :length add
  add1 >:find-address
  :find-address
  0 cursor->next-matching-byte
  :find-address sub
  >:find-length
  :find-address
  :find-length
  end
; define-function

: find-environment-string-variable
  << string[address, length]
     -- string[address, length], true
     -- false >>
  get-environment-string-variable-list drop
  find-environment-string-variable,loop
  end
; define-function

init-pid
init-command-line
init-environment-string-variable-list

512 allocate-memory
: *buffer,working-directory*
; define-variable

: get-working-directory
  << -- directory[address, length] >>
  512
  *buffer,working-directory*
  :syscall-number,getcwd:
  2 syscall
  dup negative? if
    "* (get-working-directory) fail" .s .l
    "  " .s error-code->string .s .l
    end
  then
  *buffer,working-directory*
  swap
  end
; define-function

: change-working-directory
  << directory-path[address, length] -- >>
  string->syscall-string
  :syscall-number,chdir:
  1 syscall
  dup negative? if
    "* (change-working-directory) fail" .s .l
    "  " .s error-code->string .s .l
    end
  then
  drop
  end
; define-function

0 : +stat,st_dev+        ; define-variable
0 : +stat,st_ino+        ; define-variable
0 : +stat,st_mode+       ; define-variable
0 : +stat,st_nlink+      ; define-variable
0 : +stat,st_uid+        ; define-variable
0 : +stat,st_gid+        ; define-variable
0 : +stat,st_rdev+       ; define-variable
0 : +stat,st_size+       ; define-variable
0 : +stat,st_blksize+    ; define-variable
0 : +stat,st_blocks+     ; define-variable
0 : +stat,st_atime+      ; define-variable
0 : +stat,st_atime_nsec+ ; define-variable
0 : +stat,st_mtime+      ; define-variable
0 : +stat,st_mtime_nsec+ ; define-variable
0 : +stat,st_ctime+      ; define-variable
0 : +stat,st_ctime_nsec+ ; define-variable
0 : +stat,__unused4+     ; define-variable
0 : +stat,__unused5+     ; define-variable
0 : +stat,end+           ; define-variable

: init,stat
  << offset -- >>
  *jo-size* 4 equal? if
    dup address +stat,st_dev+        save *jo-size* add
    dup address +stat,st_ino+        save *jo-size* add
    dup address +stat,st_mode+       save 2 add
    dup address +stat,st_nlink+      save 2 add
    dup address +stat,st_uid+        save 2 add
    dup address +stat,st_gid+        save 2 add
    dup address +stat,st_rdev+       save *jo-size* add
    dup address +stat,st_size+       save *jo-size* add
    dup address +stat,st_blksize+    save *jo-size* add
    dup address +stat,st_blocks+     save *jo-size* add
    dup address +stat,st_atime+      save *jo-size* add
    dup address +stat,st_atime_nsec+ save *jo-size* add
    dup address +stat,st_mtime+      save *jo-size* add
    dup address +stat,st_mtime_nsec+ save *jo-size* add
    dup address +stat,st_ctime+      save *jo-size* add
    dup address +stat,st_ctime_nsec+ save *jo-size* add
    dup address +stat,__unused4+     save *jo-size* add
    dup address +stat,__unused5+     save *jo-size* add
        address +stat,end+           save
    end
  then
  *jo-size* 8 equal? if
    dup address +stat,st_dev+        save *jo-size* add
    dup address +stat,st_ino+        save *jo-size* add
    <<
    note the following order changing
    this is linux' bad
    >>
    dup address +stat,st_nlink+      save 8 add
    dup address +stat,st_mode+       save 4 add

    dup address +stat,st_uid+        save 4 add
    dup address +stat,st_gid+        save 4 add
                                          4 add << padding >>
    dup address +stat,st_rdev+       save *jo-size* add
    dup address +stat,st_size+       save *jo-size* add
    dup address +stat,st_blksize+    save *jo-size* add
    dup address +stat,st_blocks+     save *jo-size* add
    dup address +stat,st_atime+      save *jo-size* add
    dup address +stat,st_atime_nsec+ save *jo-size* add
    dup address +stat,st_mtime+      save *jo-size* add
    dup address +stat,st_mtime_nsec+ save *jo-size* add
    dup address +stat,st_ctime+      save *jo-size* add
    dup address +stat,st_ctime_nsec+ save *jo-size* add
    dup address +stat,__unused4+     save *jo-size* add
    dup address +stat,__unused5+     save *jo-size* add
        address +stat,end+           save
    end
  then
  end
; define-function

0 init,stat
+stat,end+ : +stat,length+ ; define-variable

: path,nothing?
  << path[address, length] -- bool >>
  string->syscall-string
  >:syscall-path
  +stat,length+ allocate-local-memory
  >:stat-structure
  :stat-structure
  :syscall-path
  :syscall-number,stat:
  2 syscall
  -2 equal?
  end
; define-function

: path,file?
  << path[address, length] -- bool >>
  string->syscall-string
  >:syscall-path
  +stat,length+ allocate-local-memory
  >:stat-structure
  :stat-structure
  :syscall-path
  :syscall-number,stat:
  2 syscall
  dup
  negative? if
    drop
    false
    end
  then
  drop
  :stat-structure init,stat
  *jo-size* 4 equal? if
    +stat,st_mode+
    fetch-two-bytes
  then
  *jo-size* 8 equal? if
    +stat,st_mode+
    fetch-four-bytes
  then
  12 bit-right
  8#0010
  equal?
  <<
  0170000
  S_IFDIR       0040000 /* Directory.  */
  S_IFCHR       0020000 /* Character device.  */
  S_IFBLK       0060000 /* Block device.  */
  S_IFREG       0100000 /* Regular file.  */
  S_IFIFO       0010000 /* FIFO.  */
  S_IFLNK       0120000 /* Symbolic link.  */
  S_IFSOCK      0140000 /* Socket.  */
  >>
  end
; define-function

: path,directory?
  << path[address, length] -- bool >>
  string->syscall-string
  >:syscall-path
  +stat,length+ allocate-local-memory
  >:stat-structure
  :stat-structure
  :syscall-path
  :syscall-number,stat:
  2 syscall
  dup
  negative? if
    drop
    false
    end
  then
  drop
  :stat-structure init,stat
  *jo-size* 4 equal? if
    +stat,st_mode+
    fetch-two-bytes
  then
  *jo-size* 8 equal? if
    +stat,st_mode+
    fetch-four-bytes
  then
  12 bit-right
  8#0004
  equal?
  <<
  0170000
  S_IFDIR       00040000 /* Directory.  */
  S_IFCHR       00020000 /* Character device.  */
  S_IFBLK       00060000 /* Block device.  */
  S_IFREG       00100000 /* Regular file.  */
  S_IFIFO       00010000 /* FIFO.  */
  S_IFLNK       00120000 /* Symbolic link.  */
  S_IFSOCK      00140000 /* Socket.  */
  >>
  end
; define-function

: relative-path?
  << string[address, length] -- bool >>

  end
; define-function

: full-path?
  << string[address, length] -- bool >>

  end
; define-function

: relative-path->full-path
  << relative-path[address, length] -- full-path[address, length] >>

  end
; define-function

: poi,add
  << name[address, length], relative-path[address, length] -- >>

  end
; define-function

: poi,sub
  << name[address, length] -- >>

  end
; define-function

: poi,up
  << name[address, length], relative-path[address, length] -- >>

  end
; define-function

: poi,list
  << -- >>

  end
; define-function

: poi,show
  << name[address, length] -- >>

  end
; define-function

: poi,help
  << -- >>

  end
; define-function

100333 drop
13
: *name-hash-table,size*
; define-variable

*jo-size* 4 mul
: *name-hash-table,unit*
; define-variable

*name-hash-table,size*
*name-hash-table,unit* mul
allocate-memory
: *name-hash-table*
; define-variable

0
: *name-hash-table,counter*
; define-variable

: name-hash-table,hash
  << number, counter -- index >>
  add *name-hash-table,size* mod
  end
; define-function

16
: *max-carry-position*
; define-variable

: string->finite-carry-sum,loop
  << carry-sum, string[address, length], counter -- carry-sum >>
  over zero? if
    drop drop2
    end
  then
  dup *max-carry-position* greater-than? if
    drop 0 << re-start from 0 >>
  then
  xx|over|x
  string-head,char over
  2 swap power
  mul
  x|swap|xxxx add xxx|swap|x
  add1 xx|swap|x
  string-tail,char x|swap|xx
  <> string->finite-carry-sum,loop
; define-function

: string->finite-carry-sum
  << string[address, length] -- carry-sum >>
  0 xx|swap|x << carry-sum >>
  0 << counter >>
  string->finite-carry-sum,loop
  end
; define-function

: name->address
  << name -- address >>
  *name-hash-table,unit* mul
  *name-hash-table* add
  end
; define-function

: name,used?
  << name -- bool >>
  name->address
  fetch zero? false?
  end
; define-function

: name,used-as-title?
  << name -- bool >>
  name->address
  *jo-size* add
  fetch zero?
  end
; define-function

: name,fetch-string
  << name -- string[address, length] >>
  name->address
  fetch
  address->primitive-string
  end
; define-function

: name,fetch-title-index
  << name -- index >>
  name->address
  *jo-size* add
  fetch
  end
; define-function

: name,fetch-orbit-length
  << name -- length >>
  name->address
  *jo-size* add
  *jo-size* add
  fetch
  end
; define-function

: name,fetch-orbiton
  << name -- address >>
  name->address
  *jo-size* add
  *jo-size* add
  *jo-size* add
  fetch
  end
; define-function

: name,save-string
  << string[address, length], name -- >>
  *current-free-address,primitive-string-area*
  xx|swap|xx
  save-into,primitive-string-area
  swap
  name->address
  save
  end
; define-function

: name,save-title-index
  << index, name -- >>
  name->address
  *jo-size* add
  save
  end
; define-function

: name,save-orbit-length
  << index, name -- >>
  name->address
  *jo-size* add
  *jo-size* add
  save
  end
; define-function

: name,save-orbiton
  << index, name -- >>
  name->address
  *jo-size* add
  *jo-size* add
  *jo-size* add
  save
  end
; define-function

: name,no-collision?
  << name -- bool >>
  dup name,fetch-orbiton
  equal?
  end
; define-function

: name-hash-table,search,loop
  << string[address, length], number, counter
     -- name, true
     -- name, false >>
  >:counter >:number >::string
  :number :counter name-hash-table,hash
  >:name
  :number 0 name-hash-table,hash
  >:orbit
  :name name,used? false? if
    :name false
    end
  then
  :name name,fetch-string
  ::string string-equal? if
    :name true
    end
  then
  :name name,fetch-orbit-length
  :counter equal? if
    :name false
    end
  then
  ::string
  :number :counter add1
  <> name-hash-table,search,loop
; define-function

: name-hash-table,search
  << string[address, length]
     -- name, true
     -- false >>
  dup2 string->finite-carry-sum
  0 name-hash-table,search,loop
  end
; define-function

: name-hash-table,insert,loop
  << string[address, length], number, counter
     -- name, true
     -- name, false >>
  >:counter >:number >::string
  :number :counter name-hash-table,hash
  >:name
  :number 0 name-hash-table,hash
  >:orbit
  :name name,used? false? if
    ::string :name
    name,save-string
    :orbit :name
    name,save-orbiton
    :counter :orbit
    name,save-orbit-length
    1 address *name-hash-table,counter* add-save
    :name true
    end
  then
  :name name,fetch-string
  ::string string-equal? if
    :name true
    end
  then
  :counter *name-hash-table,size* equal? if
    :name false
    end
  then
  ::string
  :number :counter add1
  <> name-hash-table,insert,loop
; define-function

: name-hash-table,insert
  << string[address, length]
     -- name, true
     -- name, false >>
  dup2 string->finite-carry-sum
  0 name-hash-table,insert,loop
  end
; define-function

: string->name
  << string[address, length] -- name >>
  name-hash-table,insert
  false? if
    "* (string->name) *name-hash-table* is full!" .s .l
    end
  then
  end
; define-function

: name->string
  << name -- string[address, length]] >>
  name,fetch-string
  end
; define-function

: name-hash-table,report,orbit
  << name, counter -- >>
  over name,fetch-orbit-length
  over less-than? if
    drop2
    end
  then
  over name,fetch-string string->finite-carry-sum
  over name-hash-table,hash
  dup name,fetch-orbiton
  << name, counter, new-name, orbiton >>
  x|over|xxx name,fetch-string string->finite-carry-sum
  0 name-hash-table,hash
  equal? if
    "  {" .s
    dup write-number
    "} " .s
    name,fetch-string .s
    .l
  else
    drop
  then
  add1 <> name-hash-table,report,orbit
; define-function

: name-hash-table,report,loop
  << name -- >>
  dup *name-hash-table,size* equal? if
    drop
    end
  then
  dup name,used? if
  dup name,no-collision? if
    << * {index} string # orbit-lenght >>
    "* {" .s
    dup write-number
    "} " .s
    dup name,fetch-string .s
    " # " .s
    dup name,fetch-orbit-length
    write-number
    .l
    dup 1 name-hash-table,report,orbit
  then
  then
  add1 <> name-hash-table,report,loop
; define-function

: name-hash-table,report
  << -- >>
  0 name-hash-table,report,loop
  "* totally : " .s
  *name-hash-table,counter* write-number
  .l
  end
; define-function

: report-memory
  << -- >>
  "* *un-initialized-memory*" .s .l
  "  * size : " .s
       *size,un-initialized-memory*
       . .l
  "  * used : " .s
       *current-free-address,un-initialized-memory*
       *un-initialized-memory*
       sub . .l
  "  * free : " .s
       *size,un-initialized-memory*
       *current-free-address,un-initialized-memory*
       *un-initialized-memory*
       sub sub . .l
  "* *primitive-string-area*" .s .l
  "  * size : " .s
       *size,primitive-string-area*
       . .l
  "  * used : " .s
       *current-free-address,primitive-string-area*
       *primitive-string-area*
       sub . .l
  "  * free : " .s
       *size,primitive-string-area*
       *current-free-address,primitive-string-area*
       *primitive-string-area*
       sub sub . .l
  "* *jo-area*" .s .l
  "  * size : " .s
       *size,jo-area* . .l
  "  * used : " .s
       *current-free-address,jo-area*
       *jo-area*
       sub . .l
  "  * free : " .s
       *size,jo-area*
       *current-free-address,jo-area*
       *jo-area*
       sub sub . .l
  end
; define-function

: report-platform
  << -- >>
  "* platform : " .s
  platform .s
  .l
  end
; define-function

: report-jo-size
  << -- >>
  "* jo-size : " .s
  *jo-size* write-number
  " bytes" .s
  .l
  end
; define-function

: report-machine-word-size
  << -- >>
  "* machine-word-size : " .s
  *jo-size* 8 mul write-number
  " bits" .s
  .l
  end
; define-function

: report-syntax
  << -- >>
  "* *rule-set,make-jojo* :" .s .l
   *rule-set,make-jojo* rule-set,list
  "* *rule-set,basic-REPL* :" .s .l
   *rule-set,basic-REPL* rule-set,list
  "* tos-syntax-stack :" .s .l
   tos-syntax-stack rule-set,list
  end
; define-function

: initial-report
  << -- >>
  "* initial-report : " .s .l
  "  " .s report-platform
  "  " .s report-machine-word-size
  "  " .s report-jo-size
  end
; define-function

: welcome
  << -- >>
  "* welcome to cicada-nymph" .s .l
  end
; define-function

0
: *hi,random-base*
; define-variable

: hi,random
  << -- random-number >>
  0 :syscall-number,times:
  1 syscall
  9 mod
  <<
   *hi,random-base*
   *hi,random-base* add1 13 mod
   address *hi,random-base* save
  >>
  end
; define-function

: hi,say
  << number -- >>
  dup 0 equal? if drop "  cica cica da yaya !!!" .s .l end then
  dup 1 equal? if drop "  hi ^-^" .s .l end then
  dup 2 equal? if drop "  hello :)" .s .l end then
  dup 3 equal? if drop "  hey *^-^*" .s .l end then
  dup 4 equal? if drop "  hiya \^o^/" .s .l end then
  dup 5 equal? if drop "  I wish you a lovely day" .s .l end then
  dup 6 equal? if drop "  I wish you a lovely day { or night :P }" .s .l end then
  dup 7 equal? if drop "  o.o" .s .l end then
                  drop "  lovely ^3^" .s .l end
; define-function

: hi
  << -- >>
  hi,random
  hi,say
  end
; define-function

: help
  << -- >>
  "* helpful documentations will be written soon" .s .l
  "  please wait ^-^" .s .l
  end
; define-function

: command-line,unknow-function
  << -- >>
  "* (cicada-nymph) unknow command-line-function : " .s
  get-command-line
  string-tail,word
  string-head,word .s .l
  "  good bye ^-^/" .s .l
  bye
  end
; define-function

: command-line,stack-REPL
  << -- >>
  welcome
  hi
  initial-report
  "* you are in the (stack-REPL)" .s .l
  "  every time after a word is executed" .s .l
  "  the argument-stack get printed" .s .l
  print-argument-stack,pretty
  jo stack-REPL
  reset-top-level-REPL
; define-function

: command-line,basic-REPL
  << -- >>
  welcome
  hi
  initial-report
  "* you are in the (basic-REPL)" .s .l
  "  this REPL does not print any thing automaticly" .s .l
  jo basic-REPL
  reset-top-level-REPL
; define-function

: command-line,poi
  << -- >>
  "* poi byebye" .s .l
  bye
  end
; define-function

: command-line,no-function
  << -- >>
  <> command-line,stack-REPL
; define-function

: command-line-dispatcher
  << -- >>
  get-command-line
  string-tail,word
  dup2 space-string? if
    drop2
    <> command-line,no-function
  then
  string-head,word
  >::1st-word
  ::1st-word "basic-REPL" string-equal? if
    <> command-line,basic-REPL
  then
  ::1st-word "stack-REPL" string-equal? if
    <> command-line,stack-REPL
  then
  ::1st-word "poi" string-equal? if
    <> command-line,poi
  then
    <> command-line,unknow-function
; define-function

: the-story-begin
  << -- >>
  <> command-line-dispatcher
; define-function

the-story-begin
